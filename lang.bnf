/* Lexical Part */

terminator: ';';

!whitespace : ' ' | '\t' | '\r' | '\n' ;

_letter        : _unicodeLetter | '_' ;
_unicodeLetter : 'a' - 'z' | 'A' - 'Z' ;
_decimalDigit  : '0' - '9' ;

intLit: '0' | '1'-'9' {_decimalDigit}; 

identifier : _letter { _letter | _decimalDigit } ;

assign: '=';
plus: '+';
minus: '-';
mul: '*';
and: '&';

lbrace: '{';
rbrace: '}';

!comment : _lineComment | _blockComment;
_lineComment : '/' '/' { . } '\n' ;
_blockComment : '/' '*' { . | '*' } '*' '/';

/* Syntax Part */

<< 
import (
	"minicompiler/ast" 
)
>>


Program: Statements << ast.NewProgram($0) >>;

Statements
	: Statements Statement << ast.AppendStatement($0, $1) >>
	| empty << ast.NewStatementList() >>;

StatementBlock
	: lbrace Statements rbrace << ast.NewBlockStatement($1) >>;

Statement
	: "@" identifier assign Expression terminator << ast.NewIdentInit($1, $3) >>
	| "@" identifier "[" intLit "]" assign intLit terminator << ast.NewTabInit($1, $3, $6) >>
	| "if" Expression StatementBlock ElseBlock << ast.NewIfStatement($1, $2, $3) >> 
	| "while" Expression StatementBlock << ast.NewWhileStatement($1, $2) >> 
	| identifier assign Expression terminator << ast.NewAssignStatement($0, $2) >>
	| "wait" "(" intLit ")" terminator << ast.NewWaitStatement($2) >>
	| identifier "[" Expression "]" assign Expression terminator << ast.NewAssignTabStatement($0, $2, $5) >>;

ElseBlock
	: "else" StatementBlock << $1, nil >>
	| empty;

Expression
  	: Expression Operation Term << ast.NewInfixExpression($0, $2, $1) >>
  	| Term;

Term
	: intLit << ast.NewIntegerLiteral($0) >>
	| identifier "[" Expression "]" << ast.NewTabExpression($0, $2) >>
	| identifier << ast.NewIdentExpression($0) >>;

Operation
 	: plus 
 	| minus
 	| mul 
 	| and 
	| "=="
	| "!="
	| "<";
